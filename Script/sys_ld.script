
MEMORY
{
  vmRAM    (rwx) : ORIGIN = 0x00300000, LENGTH = 2048K
  vmROM    (rx ) : ORIGIN = 0x00100000, LENGTH = 2048K
}

OUTPUT_ARCH(arm)
ENTRY(_init)
SECTIONS
{

	.init.text :
	{ 
		*(.init)
		PROVIDE(__ctors_start__ = .);
		KEEP (*(SORT(.init_array.*)))
		KEEP (*(.init_array))
		PROVIDE(__ctors_end__ = .);	
		
	} >vmROM
	.init.data :
	{
		*(.init.data)
	} >vmROM
	
	.text :	
	{
		*(.text)
	} >vmROM
	
	.rodata : 
	{
		*(.rodata) 
		rodata_end = .;
	} >vmROM

	.ARM.exidx : {
	    __exidx_start = .;
	    *(.ARM.exidx* .gnu.linkonce.armexidx.*)
	    __exidx_end = .;
	} >vmROM

	.data : 
	{
		data_start = .;
		*(.data .data.* .gnu.linkonce.d.*)
		data_end = .;
	} >vmRAM AT >vmROM

    data_size = SIZEOF(.data);
    data_load_start = LOADADDR(.data);

    .bss : {
		. = ALIGN(4);
        _sbss = ABSOLUTE(.);
        *(.bss .bss.*)
        *(.gnu.linkonce.b.*)
        *(COMMON)
        . = ALIGN(4);
        _ebss = ABSOLUTE(.);
    } > vmRAM
 
	
	__HEAP_START = .;

}
